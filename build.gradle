plugins {
    id 'java-library'
    id 'jacoco'
    id 'pmd'
    id 'idea'
    id 'org.kordamp.gradle.jdeps' version '0.5.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven'
    id 'maven-publish'
}

repositories {
    jcenter()
}

dependencies {
    // As of December 29th, 2018, GSON is not yet modular.
    // implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
}

// JAVA VERSION
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// PMD
pmd {
    ignoreFailures = true
    rulePriority = 4
}

// FOR JAVA MODULE SYSTEM (JPMS)
ext.moduleName = 'com.github.joostvdg.dui.logging'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
            '--add-modules', 'junit',
            '--add-reads', "$moduleName=junit",
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

// BINTRAY PUBLISH
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            groupId 'com.github.joostvdg.dui'
            artifactId 'dui-lib-logging'
            version '1.1.0'
        }
    }
}

group =  'com.github.joostvdg.dui'
version = '1.1.0'

// publications = ['mavenPublication']

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    publications = ['MyPublication']
    pkg {
        // https://dl.bintray.com/joostvdg/maven-demo-lib
        repo = 'maven-demo-lib'
        name = 'dui-lib-logging'
        userOrg = 'joostvdg'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/joostvdg/dui-lib-logging.git'
        version {
            name = '1.1.0'
            desc = 'Build 1.1.0 - added json logger'
            released  = new Date()
        }
    }
}
